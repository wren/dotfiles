#!/usr/bin/env zsh
set -e

# Config Options
DOTFILES_REPO="https://github.com/wren/dotfiles.git"
DOTBOT_REPO="https://github.com/wren/dotfiles-dotbot.git"

DOTFILES_DIR="${0:A:h}"
DOTBOT_CONFIG="$DOTFILES_DIR/config/dotfiles/dotbot.yaml"
DOTBOT_DIR="${XDG_CACHE_HOME:-$HOME/.cache}/dotbot"
DOTBOT_PLUGINS_DIR="$DOTBOT_DIR/plugins"
DOTBOT_BIN="$DOTBOT_DIR/dotbot/bin/dotbot"

# Check dotbot directory
if ! git -C $DOTBOT_DIR rev-parse; then
  mkdir -p $DOTBOT_DIR
  git clone $DOTBOT_REPO $DOTBOT_DIR
fi

# Make sure dotbot is same version across machines
cd $DOTBOT_DIR
git pull --force origin main
git submodule update --init --recursive

# Prep dotfiles dir
export DOTFILES_DIR

# Check dotfiles directory
if ! git -C $DOTFILES_DIR rev-parse; then
  mkdir -p $DOTFILES_DIR
  git clone $DOTFILES_REPO $DOTFILES_DIR
fi
cd $DOTFILES_DIR

# Load all plugins, and run dotbot
CMD=$DOTBOT_BIN
for dir in $DOTBOT_PLUGINS_DIR/*; do
  CMD+=" --plugin-dir=$dir"
done

# Run global dotbot config first
print -P "%F{004}───── $DOTBOT_CONFIG ─────%f"
${=CMD} -d $DOTFILES_DIR -c $DOTBOT_CONFIG "$@" || true

# Then run any individual ones from various directories
set +e # if any steps fail, then the remaining steps should still happen
for file in $(find . -name 'install.conf.yaml'); do
  print -P "\n\n%F{004}───── $file ─────%f"
  ${=CMD} -d $(dirname $file) -c $file "$@"
done
