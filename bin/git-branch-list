#!/usr/bin/env zsh


# colors
color_local_branch=3
color_remote_branch=14


zparseopts -D a=all

format=(
  'branch_name=%(refname:short)'
  'is_head=%(HEAD)'
  'branch_type=%(refname:rstrip=-2)'
  'date_committed=%(committerdate:relative)'
  'subject=%(subject)'
  'author=%(authorname)'
  'push=%(push)'
  'tracking=%(upstream:track,nobracket)'
)

search_pattern=(
  "refs/heads/**/*${1}*"
)
# Show all branches (include remote)?
[[ -n $all ]] && search_pattern+=("refs/remotes/**/*${1}*")


make_report() {
  git for-each-ref --shell --format="$format" --sort='refname:rstrip=2' --sort='refname:rstrip=-2' $search_pattern | \
  while read git_info; do
    eval $git_info

    branch_type=${branch_type#refs/}
    prefix=${(M)branch_name##*/}
    bullet='·'

    # new section
    if [[ $prefix != $old_prefix ]]; then
      print "\n $prefix"
    fi

    # is this a remote?
    output_color=$color_local_branch
    if [[ $branch_type = 'remotes' ]]; then
      output_color=$color_remote_branch
    fi

    branch_color_str="%F{$output_color}"
    branch_name=${branch_name##*/}
    if [[ -z $push && $branch_type = 'heads' ]]; then
      branch_name+=' %F{#DB615E}%f'
    else
      tracking=${tracking//behind /"%F{red}"}
      tracking=${tracking//ahead /"%F{112}"}
      tracking=${tracking//, }
    fi

    # special handling for current branch
    if [[ $is_head = \* ]]; then
      bullet='▶'
      branch_color_str="%K{$output_color}%F{16}"
      tracking=''
    fi

    start_str=" ${indent}${bullet} "
    if [[ -z $prefix ]]; then
      start_str=''
    fi

    # "\$(git config branch.%(refname:short).note)";

    # output the thing
    print -P "${start_str}${branch_color_str} ${branch_name} $tracking|%f%k %F{255}${date_committed}%f|%F{141}$subject%f|%F{247}by ${author}%f"

    old_prefix=$prefix
  done
}

if command -v /usr/local/opt/util-linux/bin/column >/dev/null; then
  # preserves empty lines which looks nicer
  column -ts'|' <(make_report "$@") --table-empty-lines
else
  column -ts'|' <(make_report "$@")
fi

